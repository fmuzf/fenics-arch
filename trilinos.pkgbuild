# Maintainer: Myles English <myles at rockhead.biz>
# Contributor: Feng Wang <wanng.fenng@gmail.com>
pkgname=trilinos
pkgver=11.0.3
pkgrel=1
pkgdesc="An effort to develop algorithms and enabling technologies within an object-oriented software framework for the solution of large-scale, complex multi-physics engineering and scientific problems."
arch=('i686' 'x86_64')
url="http://trilinos.sandia.gov/index.html"
license=('LGPL3')
depends=('lapack' 'openmpi' 'python2')
source=(http://trilinos.sandia.gov/download/files/${pkgname}-${pkgver}-Source.tar.bz2)
makedepends=('python2' 'python2-numpy' 'swig' 'gcc' 'openmpi' 'perl' 'blas' 'lapack' 'atlas-lapack' 'cmake')
md5sums=('edd4cbb84ff5874dc96e846ceeab50b7')

build() {
  cd "$srcdir/$pkgname-$pkgver-Source"

  find ${srcdir} -name "*" -type f -exec \
      sed -i 's#\(/usr/bin/env \|/usr/bin/\)python[2-3]*#\1python2#' {} \;

  [[ -e build ]] || mkdir build 
  cd build

  cmake .. \
      -DCMAKE_BUILD_TYPE:STRING=DEBUG \
      -DTrilinos_ENABLE_OpenMP:BOOL=ON \
      -DTPL_ENABLE_MPI:BOOL=ON \
      -DTrilinos_ENABLE_ALL_PACKAGES:BOOL=ON \
      -DTrilinos_ENABLE_ALL_OPTIONAL_PACKAGES:BOOL=ON \
      -DML_ENABLE_Aztec:BOOL=ON \
      -DTrilinos_ENABLE_PyTrilinos:BOOL=ON \
      -DTrilinos_ENABLE_TESTS:BOOL=OFF \
      -DBUILD_SHARED_LIBS:BOOL=ON \
      -DPYTHON_EXECUTABLE:PATH=/usr/bin/python2 \
      -DML_ENABLE_MLapi:BOOL=ON \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr \
      -DCMAKE_C_FLAGS:STRING="-lpython2.7" \
      -DCMAKE_CXX_FLAGS:String="-lpython2.7" \
      -DCMAKE_Fortran_FLAGS:String="-lpython2.7" \
      $EXTRA_ARGS

  make
}

package() {
    cd $srcdir/$pkgname-$pkgver-Source/build
    make install DESTDIR=$pkgdir

    mkdir -p ${pkgdir}/etc/profile.d
    echo "export TRILINOS_DIR=/usr" > ${pkgdir}/etc/profile.d/trilinos.sh
    chmod +x ${pkgdir}/etc/profile.d/trilinos.sh
}
