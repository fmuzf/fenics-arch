# Maintainer: Myles English <myles at rockhead dot biz>
pkgname=ufc-bzr
pkgver=355
pkgrel=1
pkgdesc="A unified framework for finite element assembly."
arch=('any')
url="http://launchpad.net/ufc"
license=('LGPL')
groups=('fenics-bzr')
depends=('python2') # 'boost' 'swig')
makedepends=('bzr' 'python2')
provides=('ufc')
conflicts=('ufc')
options=(!emptydirs)

_bzrtrunk=lp:ufc
_bzrmod=ufc

build() {
    msg "Connecting to Bazaar server...."

    if [ -d $_bzrmod ]; then
	cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver && cd ..
	msg "The local files are updated."
    else
	bzr branch $_bzrtrunk $_bzrmod -r $pkgver
    fi

    msg "BZR checkout done or server timeout"
    msg "Starting make..."

    [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
    cp -r $_bzrmod $_bzrmod-build
    cd $_bzrmod-build

    # replace python with python2 everywhere
    find ./ -name "*" -type f -exec \
	sed -i 's#\(/usr/bin/env \|/usr/bin/\)python[2-3]*#\1python2#' {} \;

    [[ -e build ]] || mkdir build 
    cd build

    # get the real path to the python executable, allows for virtualenv setups
    local py2_interp=`python2 -c "import os,sys; print(os.path.realpath(sys.executable))"`
    local py2_inc="${py2_interp/\/bin\///include/}"
    local py2_lib=`ldd ${py2_interp} | grep python | cut -d " " -f 3 | sed 's/\(.*.so\).*/\1/'`

    #TODO: should this be? cmake .. -DCMAKE_INSTALL_PREFIX=${pkg}/usr \
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
	     -DPYTHON_EXECUTABLE:PATH=${py2_interp} \
	     -DPYTHON_INCLUDE_DIR:PATH=${py2_inc} \
             -DPYTHON_LIBRARY:FILEPATH=${py2_lib}
    make
}

package() {
    cd $srcdir/$_bzrmod-build/build
    make install DESTDIR=$pkgdir
}

# vim:set ts=2 sw=2 et:
